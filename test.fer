let io = import('std/io');
let fs = import('std/fs');
let os = import('std/os');
let argparse = import('std/argparse');

let ssh = import('ssh/ssh');

let try = fn(res, msg...) {
    io.print('==> ', msg..., ' ... ');
    if res.isOk() {
        io.println('success!');
        return res.getVal();
    }
    io.println('failed!');
    raise('error (', res.getVal().getCode(), '): ', res.getVal().getMsg());
};

let args = argparse.new(feral.args);
args.addArg('host').setValReqd(true).setReqd(true).setHelp('Hostname to connect to');
args.addArg('port').setValReqd(true).setReqd(true).setHelp('Port on the host to connect to');
args.parse();

let conn = ssh.new();
try(conn.setOpt(ssh.OPT_HOST, args.getValue('host')), 'setting host');
try(conn.setOpt(ssh.OPT_PORT, args.getValue('port').int()), 'setting port');
try(conn.setOpt(ssh.OPT_LOG_VERBOSITY, ssh.LOG_FUNCTIONS), 'setting log verbosity');
try(conn.connect(), 'connecting');
let key = try(conn.getServerPublicKey(), 'retrieving public key');
let hash = try(key.getHash(), 'getting key hash');
let knownHostsState = conn.isKnownServer();
io.println('==> connection known hosts state: ', ssh.knownHostsStateStr(knownHostsState));
try(ssh.verifyKnownHost(conn), 'verifying known hosts');
try(ssh.authenticate(conn), 'authenticating');
io.print('==> getting banner ... ');
let banner = conn.getIssueBanner();
io.println('success!');
io.println('==> banner: ', banner);
conn.disconnect();
io.println('==> disconnected!');